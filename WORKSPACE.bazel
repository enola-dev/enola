# SPDX-License-Identifier: Apache-2.0
#
# Copyright 2023-2024 The Enola <https://enola.dev> Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# see ./tools/java_toolchain/BUILD
# see https://github.com/bazelbuild/bazel/issues/20877, NB the "_definition" suffix!
register_toolchains("//tools/java_toolchain:repository_default_java_toolchain_definition")

load("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

OUR_GUAVA_VERSION = "33.3.1"

GRPC_JAVA_VERSION = "1.69.0"

# NOTE: The GRPC_GUAVA_VERSION needs to be adapted when GRPC_JAVA_VERSION (above) is bumped:
GRPC_GUAVA_VERSION = "33.3.1"

RDF4J_VERSION = "5.0.2"

# https://github.com/grpc/grpc-java/
# https://github.com/enola-dev/enola/issues/202
# https://github.com/grpc/grpc-java/issues/10215
# TODO replace with bazel_dep in MODULE.bazel
# when https://github.com/bazelbuild/bazel-central-registry/issues/353
# (AKA https://github.com/grpc/grpc-java/issues/9559) is available.
http_archive(
    name = "io_grpc_grpc_java",
    sha256 = "3874ac920ad86b7a096e94c0fb17e52a9ed00982bf1fb44910d63bd9345a2fe4",
    strip_prefix = "grpc-java-%s" % GRPC_JAVA_VERSION,
    url = "https://github.com/grpc/grpc-java/archive/refs/tags/v%s.zip" % GRPC_JAVA_VERSION,
)

load("@io_grpc_grpc_java//:repositories.bzl", "IO_GRPC_GRPC_JAVA_ARTIFACTS", "IO_GRPC_GRPC_JAVA_OVERRIDE_TARGETS")

# https://rules-proto-grpc.com/en/latest/#installation
http_archive(
    name = "rules_proto_grpc",
    sha256 = "38a51a3183dffa5323bbf07b7d5473fa1f06707cf59607888f36cb735ab39cd8",
    strip_prefix = "rules_proto_grpc-5.0.1",
    urls = ["https://github.com/rules-proto-grpc/rules_proto_grpc/releases/download/5.0.1/rules_proto_grpc-5.0.1.tar.gz"],
)

load("@rules_proto_grpc//:repositories.bzl", "rules_proto_grpc_repos", "rules_proto_grpc_toolchains")

rules_proto_grpc_toolchains()

rules_proto_grpc_repos()

load("@rules_proto//proto:repositories.bzl", "rules_proto_dependencies", "rules_proto_toolchains")

rules_proto_dependencies()

rules_proto_toolchains()

# https://rules-proto-grpc.com/en/latest/lang/doc.html#doc-markdown-compile
load("@rules_proto_grpc//doc:repositories.bzl", rules_proto_grpc_doc_repos = "doc_repos")

rules_proto_grpc_doc_repos()

# https://rules-proto-grpc.com/en/latest/lang/buf.html#buf-proto-lint-test
load("@rules_proto_grpc//buf:repositories.bzl", rules_proto_grpc_buf_repos = "buf_repos")

rules_proto_grpc_buf_repos()

# https://github.com/bazelbuild/rules_jvm_external#usage
load("@rules_jvm_external//:repositories.bzl", "rules_jvm_external_deps")

rules_jvm_external_deps()

load("@rules_jvm_external//:setup.bzl", "rules_jvm_external_setup")

rules_jvm_external_setup()

# https://github.com/grpc/grpc-java/blob/master/examples/WORKSPACE
load("@rules_jvm_external//:defs.bzl", "maven_install")

# This is required so that we can use duplicate_version_warning (below) and fixes
# the "com.google.guava:guava has multiple versions x.y.z-android, x.y.z-jre" problem.
IO_GRPC_GRPC_JAVA_ARTIFACTS_WITHOUT_GUAVA = [
    item
    for item in IO_GRPC_GRPC_JAVA_ARTIFACTS
    if ((item != "com.google.guava:guava:" + GRPC_GUAVA_VERSION + "-android") and
        not item.startswith("com.google.errorprone:error_prone_annotations"))
]

maven_install(
    artifacts = IO_GRPC_GRPC_JAVA_ARTIFACTS_WITHOUT_GUAVA + [
        "junit:junit:4.13.2",
        "com.google.auto.service:auto-service:1.1.1",
        "com.google.auto.service:auto-service-annotations:1.1.1",
        "com.google.guava:guava:%s-jre" % OUR_GUAVA_VERSION,
        "com.google.jimfs:jimfs:1.3.0",
        "com.google.truth.extensions:truth-java8-extension:1.4.4",
        "com.google.truth.extensions:truth-proto-extension:1.4.4",
        # Protobuf
        "com.google.protobuf:protobuf-java:4.29.2",
        "com.google.protobuf:protobuf-java-util:4.29.2",
        # gRPC
        "io.grpc:grpc-netty:%s" % GRPC_JAVA_VERSION,
        "io.grpc:grpc-stub:%s" % GRPC_JAVA_VERSION,
        "io.grpc:grpc-inprocess:%s" % GRPC_JAVA_VERSION,
        "org.apache.tomcat:annotations-api:6.0.53",  # NOT :tomcat-annotations-api
        # RDF4j <https://rdf4j.org>
        "org.eclipse.rdf4j:rdf4j-model:%s" % RDF4J_VERSION,
        "org.eclipse.rdf4j:rdf4j-rio-jsonld:%s" % RDF4J_VERSION,
        "org.eclipse.rdf4j:rdf4j-rio-turtle:%s" % RDF4J_VERSION,
        # TODO Remove jackson-databind when https://github.com/eclipse-rdf4j/rdf4j/issues/4907 Link Bug is fixed!
        "com.fasterxml.jackson.core:jackson-databind:2.13.5",
        # kubernetes:client-java also depends on Protobuf (+gRPC?), version may need to be upgraded together
        "io.kubernetes:client-java:19.0.0",
        # Other
        "ch.vorburger.exec:exec:3.1.5",
        "com.github.java-json-tools:uri-template:0.10",
        "info.picocli:picocli:4.7.5",
        "org.snakeyaml:snakeyaml-engine:2.7",
        "org.yaml:snakeyaml:2.2",
        "org.slf4j:slf4j-jdk14:2.0.16",
        "org.slf4j:slf4j-simple:2.0.16",
        # Tika (unfortunately!) uses Log4j (v2) instead of SLF4j, so:
        "org.apache.logging.log4j:log4j-to-jul:2.24.0",
        "io.github.classgraph:classgraph:4.8.179",
        "com.google.errorprone:error_prone_annotations:2.36.0",
        "com.google.errorprone:error_prone_type_annotations:2.36.0",
        "org.jspecify:jspecify:1.0.0",
        "com.squareup.okhttp3:okhttp:4.12.0",
        "com.squareup.okhttp3:logging-interceptor:4.12.0",
        "dev.dirs:directories:26",
        "io.github.amithkoujalgi:ollama4j:1.0.70",
        "com.github.spotbugs:spotbugs-annotations:4.8.6",
        "io.github.java-diff-utils:java-diff-utils:4.12",
        "org.jsoup:jsoup:1.18.1",
        "org.apache.tika:tika-core:3.0.0-BETA2",
        "org.apache.tika:tika-parsers-standard-package:3.0.0-BETA2",
    ],
    duplicate_version_warning = "error",
    excluded_artifacts = [
        # https://commons.apache.org/proper/commons-logging/ is a PITA,
        # because it contains package org.apache.commons.logging, which we
        # already get from org.slf4j:jcl-over-slf4j, see https://www.slf4j.org/legacy.html
        "commons-logging:commons-logging",

        # ROME Utils classes appear to also be included in com.rometools:rome - at least via Tika.
        "com.rometools:rome-utils",
    ],
    fail_if_repin_required = True,
    fetch_sources = True,
    generate_compat_repositories = True,
    maven_install_json = "//:maven_install.json",
    override_targets = IO_GRPC_GRPC_JAVA_OVERRIDE_TARGETS,
    repositories = [
        "https://repo.maven.apache.org/maven2/",
    ],
)

load("@maven//:compat.bzl", "compat_repositories")

compat_repositories()

load("@maven//:defs.bzl", "pinned_maven_install")

pinned_maven_install()
